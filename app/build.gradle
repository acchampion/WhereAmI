plugins {
	id 'com.android.application'
	id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
	defaultConfig {
		compileSdk = 35
		buildToolsVersion = "35.0.0"
		applicationId("edu.ohiostate.whereami")
		minSdkVersion(26)
		targetSdkVersion(35)
		versionCode = 1
		versionName = "1.0"
		vectorDrawables.useSupportLibrary = true
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
	namespace('edu.ohiostate.whereami')
}

secrets {
	// Optionally specify a different file name containing your secrets.
	// The plugin defaults to "local.properties"
	propertiesFileName = "secrets.properties"

	// A properties file containing default secret values. This file can be
	// checked in version control.
	defaultPropertiesFileName = "local.defaults.properties"

	// Configure which keys should be ignored by the plugin by providing regular expressions.
	// "sdk.dir" is ignored by default.
	ignoreList.add("keyToIgnore") // Ignore the key "keyToIgnore"
	ignoreList.add("sdk.*")       // Ignore all keys matching the regexp "sdk.*"
}

dependencies {
	implementation(fileTree(include: ['*.jar'], dir: 'libs'))

	constraints {
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.21") {
			because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
		}
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.21") {
			because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
		}
	}
	implementation("com.google.android.gms:play-services-location:$rootProject.playServicesLocationVersion")
	implementation("com.google.android.gms:play-services-maps:$rootProject.playServicesMapsVersion")
	implementation("androidx.appcompat:appcompat:$rootProject.appcompatVersion")
	implementation("androidx.activity:activity:$rootProject.activityVersion")
	implementation("androidx.preference:preference:$rootProject.preferenceVersion")
	testImplementation("junit:junit:$rootProject.junitLibVersion")
	androidTestImplementation("androidx.test.ext:junit:$rootProject.junitVersion")
	androidTestImplementation("androidx.test.espresso:espresso-core:$rootProject.espressoVersion")
}
